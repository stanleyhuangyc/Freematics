/******************************************************************************
* Reference sketch for a vehicle telematics data feed for Freematics Hub
* Works with Freematics ONE
* Developed by Stanley Huang <stanley@freematics.com.au>
* Distributed under BSD license
* Visit https://freematics.com/hub for information about Freematics Hub
* Visit https://freematics.com/products for hardware information
*
* THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
* IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
* FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
* AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
* LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
* OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
* THE SOFTWARE.
******************************************************************************/

#include <FreematicsONE.h>
#include <FreematicsNetwork.h>
#include <avr/pgmspace.h>
#include <avr/io.h>
#include <avr/interrupt.h>
#include <avr/common.h>
#include <avr/wdt.h>
#include <avr/sleep.h>
#include "telelogger.h"
#include "config.h"

// states
#define STATE_OBD_READY 0x1
#define STATE_OBD_FOUND 0x2
#define STATE_GPS_READY 0x4
#define STATE_LOCATION_READY 0x8
#define STATE_MEMS_READY 0x10
#define STATE_NET_READY 0x20
#define STATE_SERVER_CONNECTED 0x40
#define STATE_WORKING 0x80

#if MEMS_MODE
float accBias[3] = {0}; // calibrated reference accelerometer data
float accSum[3] = {0};
uint16_t accCount = 0;
#endif
char devid[8] = DEFAULT_DEVID;
uint8_t deviceTemp = 0; // device temperature

uint32_t txCount = 0;
uint8_t connErrors = 0;
uint16_t UTC = 0;
uint32_t lastSyncTime = 0;
uint32_t lastMotionTime = 0;
uint32_t lastOBDTry = 0;
uint8_t obdRetryInterval = 0;
uint32_t sessionStartTime = 0;
uint16_t stationaryTime[] = STATIONARY_TIME_TABLE;
uint8_t sendingIntervals[] = SENDING_INTERVAL_TABLE;

void recvTasks(int timeout = 0);
void processMEMS(bool process);

class State {
public:
  bool check(byte flags) { return (state & flags) == flags; }
  void set(byte flags) { state |= flags; }
  void clear(byte flags) { state &= ~flags; }
  byte state = 0;
};

State state;

#if NET_DEVICE == NET_WIFI
UDPClientESP8266AT net;
#elif NET_DEVICE == NET_SIM800
UDPClientSIM800 net;
#elif NET_DEVICE == NET_SIM5360
UDPClientSIM5360 net;
#else
UDPClient net; // null client
#endif

#if MEMS_MODE
MPU9250_ACC mems;
#endif

Cache cache;

class OBD : public COBDSPI {
protected:
  void idleTasks()
  {
    // do some quick tasks while waiting for OBD response
#if MEMS_MODE
    processMEMS(false);
#endif
  }
};

OBD obd;
#define sys obd

/*******************************************************************************
  Freematics Hub client implementation
*******************************************************************************/
bool verifyChecksum(char* data)
{
  uint8_t sum = 0;
  char *s = strrchr(data, '*');
  if (!s) return false;
  for (char *p = data; p < s; p++) sum += *p;
  if (hex2uint8(s + 1) == sum) {
    *s = 0;
    return true;
  }
  return false;
}

bool notify(byte event)
{
  cache.header(devid);
  char buf[32];

  if (event == EVENT_LOGIN) {
    if (state.check(STATE_OBD_READY)) {
      char vin[128];
      if (obd.getVIN(vin, sizeof(vin))) {
        Serial.print(F("VIN:"));
        Serial.println(vin);
        cache.dispatch(buf, snprintf_P(buf, sizeof(buf), PSTR("VIN=%s"), vin));
      }
    }
    cache.dispatch(buf, snprintf_P(buf, sizeof(buf), PSTR("DF=%u"), DEV_SIG | (unsigned int)state.state));
  }
  int rssi = net.getSignal();
  if (rssi) {
    Serial.print(F("RSSI:"));
    Serial.print(rssi);
    Serial.println(F("dBm"));
    cache.dispatch(buf, snprintf_P(buf, sizeof(buf), PSTR("SSI=%d"), rssi));
  }
  cache.dispatch(buf, snprintf_P(buf, sizeof(buf), PSTR("EV=%X"), (unsigned int)event));
  cache.dispatch(buf, snprintf_P(buf, sizeof(buf), PSTR("TS=%lu"), millis()));
  cache.tailer();

  Serial.print(F("SERVER.."));
  for (byte attempts = 0; attempts < 3; attempts++) {
    Serial.print('.');
    if (!net.send(cache.buffer(), cache.length())) {
      delay(1000);
      continue;
    }
    if (event == EVENT_ACK) {
      Serial.println(F("ACK"));
      return true; // no reply for ACK
    }
    // receive reply
    delay(1000);
    int len;
    char *data = net.receive(&len);
    if (!data) {
      // no reply yet
      
      continue;
    }
    data[len] = 0;
    // verify checksum
    if (!verifyChecksum(data)) {
      Serial.println(data);
      continue;
    }
    char pattern[16];
    snprintf_P(pattern, sizeof(pattern), PSTR("EV=%u"), event);
    if (!strstr(data, pattern)) {
      continue;
    }
    lastSyncTime = millis();
    connErrors = 0;
    // success
    Serial.println(F("OK"));
    return true;
  }
  Serial.println(F("NO"));
  return false;
}

bool login()
{
  // connect to telematics server
  for (byte attempts = 0; attempts < 3; attempts++) {
    if (!net.open(SERVER_HOST, SERVER_PORT)) {
      Serial.println(F("NO NET"));
      delay(1000);
      continue;
    }
    byte event = connErrors ? EVENT_RECONNECT : EVENT_LOGIN; 
    // login Freematics Hub
    if (!notify(event)) {
      net.close();
      delay(3000);
      continue;
    }
    return true;
  }
  return false;
}

bool transmit()
{
  bool success = false;
  unsigned long elapsed = (millis() - sessionStartTime) / 1000;
  Serial.print(elapsed / 60);
  Serial.print(':');
  Serial.print(elapsed % 60);
  Serial.print(' ');
  // transmit data
  if (net.send(cache.buffer(), cache.length())) {
    connErrors = 0;
    txCount++;
    // output some stats
    Serial.print('#');
    Serial.print(txCount);
    Serial.print(' ');
    Serial.print(cache.length());
    Serial.println(F(" bytes sent"));
    success = true;
  } else {
    connErrors++;
    Serial.print(F("NET ERR:"));
    Serial.println(connErrors);
  }
  return success;
}

bool ping()
{
  if (!net.open(SERVER_HOST, SERVER_PORT)) {
    return false;
  }
  bool success = notify(EVENT_PING);
  if (success) lastSyncTime = millis();
  return success;
}

/*******************************************************************************
  Reading and processing OBD data
*******************************************************************************/
int logOBDPID(byte pid)
{
  int value;
  if (obd.isValidPID(pid) && obd.readPID(pid, value)) {
    cache.log((uint16_t)0x100 | pid, (int16_t)value);
    return value;
  } else {
    return -1;
  }
}

void processOBD()
{
  int speed = logOBDPID(PID_SPEED);
  if (speed == -1) {
    return;
  }
  if (speed > 0) lastMotionTime = millis();
  // poll more PIDs
  const byte pids[]= {PID_RPM, PID_ENGINE_LOAD, PID_THROTTLE};
  for (byte i = 0; i < sizeof(pids) / sizeof(pids[0]); i++) {
    logOBDPID(pids[i]);
  }
  const byte pidTier2[] = {PID_INTAKE_TEMP, PID_COOLANT_TEMP, PID_BAROMETRIC, PID_AMBIENT_TEMP};
  static byte idx = 0;
  byte pid = pidTier2[idx];
  logOBDPID(pid);
  if (++idx >= sizeof(pidTier2)) idx = 0;
}

#if ENABLE_GPS
void processGPS()
{
  static uint8_t lastGPSDay = 0;
  GPS_DATA gd = {0};
  // read parsed GPS data
  if (sys.gpsGetData(&gd) && gd.sat >= 3) {
    if (gd.date && UTC != (uint16_t)gd.time) {
      byte day = gd.date / 10000;
      cache.log(PID_GPS_TIME, gd.time);
      if (lastGPSDay != day) {
        cache.log(PID_GPS_DATE, gd.date);
        lastGPSDay = day;
      }
      unsigned int kph = (unsigned long)gd.speed  * 1852 / 100000;
      if (kph >= 2) lastMotionTime = millis();
      if (gd.lat || gd.lng || gd.alt) {
        cache.logCoordinate(PID_GPS_LATITUDE, gd.lat);
        cache.logCoordinate(PID_GPS_LONGITUDE, gd.lng);
        cache.log(PID_GPS_ALTITUDE, gd.alt);
        cache.log(PID_GPS_SAT_COUNT, gd.sat);
        cache.log(PID_GPS_SPEED, kph);
        cache.log(PID_GPS_HEADING, gd.heading / 100);
      }
      Serial.print("[GPS] ");
      Serial.print(gd.lat);
      Serial.print(' ');
      Serial.print(gd.lng);
      Serial.print(' ');
      Serial.print(gd.alt);
      Serial.print("m ");
      Serial.print(kph);
      Serial.print("kph UTC:");
      Serial.print(gd.time);
      Serial.print(" SAT:");
      Serial.println((unsigned int)gd.sat);
      UTC = (uint16_t)gd.time;
    }
  }
}
#endif

void processLocation()
{
  GPS_DATA* gd = net.getLocation();
  // read parsed GPS data
  if (gd && UTC != (uint16_t)gd->time) {
    cache.log(PID_GPS_DATE, gd->date);
    cache.log(PID_GPS_TIME, gd->time);
    cache.logCoordinate(PID_GPS_LATITUDE, gd->lat);
    cache.logCoordinate(PID_GPS_LONGITUDE, gd->lng);
    cache.log(PID_GPS_ALTITUDE, gd->alt);
    unsigned int kph = (unsigned long)gd->speed * 1852 / 100000;
    cache.log(PID_GPS_SPEED, kph);
    if (kph >= 1) lastMotionTime = millis();
    cache.log(PID_GPS_HEADING, gd->heading);
    Serial.print("[GPS] ");
    Serial.print(gd->lat);
    Serial.print(' ');
    Serial.print(gd->lng);
    Serial.print(' ');
    Serial.print(gd->alt);
    Serial.print("m ");
    Serial.print(kph);
    Serial.print("kph UTC:");
    Serial.println(gd->time);
    UTC = (uint16_t)gd->time;
  }
}

#if MEMS_MODE
void processMEMS(bool process)
{
  if (!state.check(STATE_MEMS_READY)) return;

  // load and store accelerometer data
  int16_t temp = 0;
  float acc[3];
  if (!mems.read(acc, 0, 0, &temp)) return;
  accSum[0] += acc[0];
  accSum[1] += acc[1];
  accSum[2] += acc[2];
  accCount++;

  if (process) {
    if (accCount) {
      acc[0] = accSum[0] / accCount;
      acc[1] = accSum[1] / accCount;
      acc[2] = accSum[2] / accCount;
      cache.log(PID_ACC, (int16_t)((acc[0] - accBias[0]) * 100), (int16_t)((acc[1] - accBias[1]) * 100), (int16_t)((acc[2] - accBias[2]) * 100));
      temp /= 10;
      if (temp != deviceTemp) {
        cache.log(PID_DEVICE_TEMP, deviceTemp = temp);
      }
      // calculate instant motion
      float motion = 0;
      for (byte i = 0; i < 3; i++) {
        float m = (acc[i] - accBias[i]);
        motion += m * m;
      }
      if (motion >= MOTION_THRESHOLD * MOTION_THRESHOLD) {
        lastMotionTime = millis();
      }
    }
    accSum[0] = 0;
    accSum[1] = 0;
    accSum[2] = 0;
    accCount = 0;
  }
}

void calibrateMEMS()
{
  accBias[0] = 0;
  accBias[1] = 0;
  accBias[2] = 0;
  int n;
  for (n = 0; n < 100; n++) {
    float acc[3] = {0};
    mems.read(acc);
    accBias[0] += acc[0];
    accBias[1] += acc[1];
    accBias[2] += acc[2];
    delay(10);
  }
  accBias[0] /= n;
  accBias[1] /= n;
  accBias[2] /= n;
}
#endif

/*******************************************************************************
  Initializing all components and network
*******************************************************************************/
bool initialize()
{
  state.clear(STATE_WORKING);

#if MEMS_MODE
  if (!state.check(STATE_MEMS_READY)) {
    Serial.print(F("MEMS:"));
    if (mems.begin()) {
      state.set(STATE_MEMS_READY);
      Serial.println(F("OK"));
    } else {
      Serial.println(F("NO"));
    }
  }
#endif

  net.begin(&sys, true);
  
  // initialize OBD communication
  if (!state.check(STATE_OBD_READY)) {
    Serial.print(F("OBD:"));
    if (!obd.init()) {
      Serial.println(F("NO"));
      if (state.check(STATE_OBD_FOUND)) return false;
    } else {
      Serial.println(F("OK"));
      state.set(STATE_OBD_READY | STATE_OBD_FOUND);
    }
    obdRetryInterval = 10;
  }

#if ENABLE_GPS
  // start serial communication with GPS receiver
  if (!state.check(STATE_GPS_READY)) {
    Serial.print(F("GPS:"));
    if (sys.gpsInit(GPS_SERIAL_BAUDRATE)) {
      state.set(STATE_GPS_READY);
      Serial.println(F("OK"));
    } else {
      Serial.println(F("NO"));
    }
  }
#endif

  // initialize network module
  if (!state.check(STATE_NET_READY)) {
    Serial.print(F("CELL:"));
    if (net.begin(&sys)) {
      Serial.println(F("OK"));
      state.set(STATE_NET_READY);
    } else {
      Serial.println(F("NO"));
      return false;
    }
  }

#if NET_DEVICE == NET_SIM5360
  // retrieve cell module info
  char *info = net.getBuffer();
  char IMEI[16] = {0};
  char *p = strstr_P(info, PSTR("IMEI:"));
  if (p) {
    char *q = strchr(p, '\r');
    if (q) *q = 0;
    strncpy(IMEI, p + 6, sizeof(IMEI) - 1);
  }
  Serial.println(strstr_P(info, PSTR("Rev")));
  // generate device ID from IMEI
  uint32_t seed = atol(IMEI + 5);
  for (byte i = 0; i < 7; i++, seed >>= 5) {
    byte x = (byte)seed & 0x1f;
    if (x >= 10) {
      x = x - 10 + 'A';
      switch (x) {
        case 'B': x = 'W'; break;
        case 'D': x = 'X'; break;
        case 'I': x = 'Y'; break;
        case 'O': x = 'Z'; break;
      }
    } else {
      x += '0';
    }
    devid[i] = x;
  }
  devid[7] = 0;
#endif

  Serial.print(F("DEVID:"));
  Serial.println(devid);

#if NET_DEVICE == NET_WIFI
  for (byte attempts = 0; attempts < 3; attempts++) {
    Serial.print(F("WIFI(SSID:"));
    Serial.print(WIFI_SSID);
    Serial.print(F("):"));
    if (net.setup(WIFI_SSID, WIFI_PASSWORD)) {
      Serial.println(F("OK"));
      state.set(STATE_NET_READY);
      break;
    } else {
      Serial.println(F("NO"));
    }
  }
  if (!state.check(STATE_NET_READY)) {
    return false;
  }
#else
  Serial.print("SIM:");
  bool hasSIM = net.checkSIM();
  if (hasSIM) {
    Serial.println(F("OK"));
  } else {
    Serial.println(F("NO"));
  }

  Serial.print(F("NET.."));
  if (net.setup(CELL_APN, hasSIM ? 30000 : 1000, !state.check(STATE_GPS_READY))) {
    String op = net.getOperatorName();
    if (op.length()) {
      Serial.println(op);
    } else {
      Serial.println(F("OK"));
    }
    state.set(STATE_SERVER_CONNECTED);
    login();
  } else {
    Serial.println(F("NO"));
    if (hasSIM) return false;
  }
  if (net.getLocation()) {
    Serial.println(F("CELL GPS ON"));
  }
#endif

#if MEMS_MODE
  if (state.check(STATE_MEMS_READY)) {
    calibrateMEMS();
  }
#endif

#if 0
  Serial.print(F("IP:"));
  String ip = net.getIP();
  if (ip.length()) {
    Serial.println(ip);
  } else {
    Serial.println(F("NO"));
  }
#endif

  txCount = 0;

  sessionStartTime = lastMotionTime = millis();
  state.set(STATE_WORKING);
  return true;
}

void shutDownNet()
{
  //obd.checkConn();
  Serial.print(F("CELL:"));
  net.close();
  net.end();
  Serial.println(F("OFF"));
  state.clear(STATE_NET_READY | STATE_SERVER_CONNECTED);
}

bool waitMotion(long timeout)
{
  unsigned long t = millis();
#if MEMS_MODE
  if (state.check(STATE_MEMS_READY)) {
    while ((long)(millis() - t) < timeout) {
      delay(100);
      // calculate relative movement
      float motion = 0;
      float acc[3];
      int16_t temp = 0;
      mems.read(acc, 0, 0, &temp);
      for (byte i = 0; i < 3; i++) {
        float m = (acc[i] - accBias[i]);
        motion += m * m;
      }
      // check movement
      if (motion >= MOTION_THRESHOLD * MOTION_THRESHOLD) {
        lastMotionTime = millis();
        return true;
      }
    }
    return false;
  }
#endif
  do {
    long elapsed = millis() - t;
    if (elapsed >= timeout) break;
    delay(min(5000, timeout - elapsed));
  } while (obd.getVoltage() < CHARGING_VOLTAGE);
  return true;
}

/*******************************************************************************
  Collecting and processing data
*******************************************************************************/
void process()
{
  cache.header(devid);
  cache.timestamp(millis());

  // process GPS data if connected
  if (state.check(STATE_GPS_READY)) {
#if ENABLE_GPS
    processGPS();
#endif
  } else {
    processLocation();
  }

  // process OBD data if connected
  if (state.check(STATE_OBD_READY)) {
    processOBD();
    if (obd.errors >= MAX_OBD_ERRORS) {
      state.clear(STATE_WORKING);
      return;
    }
  }

  // read and log car battery voltage, data in 0.01v
  float volts = obd.getVoltage();
  if (volts) {
    cache.log(PID_BATTERY_VOLTAGE, (unsigned int)(volts * 100)); /* in 0.01V */
  }
  // when battery voltage gets low, enter standby mode
  if (volts >= 6 && volts < BATTERY_LOW_VOLTAGE) {
    Serial.print(F("LOW BATT:"));
    Serial.println(volts, 1);
    state.clear(STATE_WORKING);
  }

#if MEMS_MODE
  processMEMS(true);
#endif

  cache.tailer();
}

/*******************************************************************************
  Implementing stand-by mode
*******************************************************************************/
void sleep(uint8_t period)
{
  wdt_enable(period);
  wdt_reset();
  WDTCSR |= _BV(WDIE);
  set_sleep_mode(SLEEP_MODE_PWR_DOWN);
  sleep_mode();
  wdt_disable();
  WDTCSR &= ~_BV(WDIE);
}

void standby()
{
  unsigned long t = millis();
#if ENABLE_GPS
  if (state.check(STATE_GPS_READY)) {
    Serial.println(F("GPS OFF"));
    sys.gpsInit(0); // turn off GPS power
  }
#endif
  UTC = 0;
  state.clear(STATE_OBD_READY | STATE_GPS_READY);
  for (;;) {
    if (state.check(STATE_NET_READY)) shutDownNet();
#if MEMS_MODE
    calibrateMEMS();
#endif
    Serial.println(F("STANDBY"));
    int ret = 0;
    if (state.check(STATE_MEMS_READY)) {
      ret = waitMotion(1000L * PING_BACK_INTERVAL - (millis() - t));
    } else {
      do {
        float volts = obd.getVoltage();
        Serial.println(volts, 1);
        if (volts >= CHARGING_VOLTAGE) {
          ret = 1;
          break;
        }
        delay(5000);
      } while (millis() - t < 1000L * PING_BACK_INTERVAL);
    }
    Serial.println(F("WAKEUP"));
    t = millis();
    obd.getVersion();
    Serial.println();
    if (ret) break;
    // check battery voltage
    float volts = obd.getVoltage();
    if (volts >= 6 && volts < BATTERY_LOW_VOLTAGE) {
      Serial.print(F("LOW BATT:"));
      Serial.println(volts, 1);
      // sleep 120 seconds
      for (byte n = 0; n < 15; n++) sleep(WDTO_8S);
      continue;
    }
    // start ping
    Serial.print(F("Ping..."));
    net.begin(&sys, true);
    cache.header(devid);
    cache.timestamp(millis());
    cache.log(PID_DEVICE_TEMP, deviceTemp);
    cache.log(PID_BATTERY_VOLTAGE, (uint16_t)(volts * 100));
    cache.tailer();
#if NET_DEVICE == NET_WIFI
    if (!net.setup(WIFI_SSID, WIFI_PASSWORD))
#else
    if (!net.begin(&sys) || !net.setup(CELL_APN, 15000, false))
#endif
    {
      Serial.println(F("NO"));
      continue;
    }
    Serial.println(net.getIP());
    state.set(STATE_NET_READY);
    if (ping()) {
      state.set(STATE_SERVER_CONNECTED);
      // ping back data
      transmit();
    }
  }
#if RESET_AFTER_WAKEUP
  delay(100);
  void(* resetFunc) (void) = 0;
  resetFunc();
#endif  
}

/*******************************************************************************
  Tasks to perform in idle/waiting time
*******************************************************************************/
void recvTasks(int timeout)
{
  if (state.check(STATE_NET_READY)) {
     // check incoming datagram
    int len = 0;
    char *data = net.receive(&len, timeout);
    if (data) {
      data[len] = 0;
      Serial.print(F("RECV:{"));
      Serial.print(data);
      Serial.println('}');
      if (!verifyChecksum(data)) {
        return;
      }
      char *p = strstr_P(data, PSTR("EV="));
      if (p) {
        byte eventID = atoi(p + 3);
        if (eventID) lastSyncTime = millis();
      }
    }
  }
}

void setup()
{
  // Disable the ADC
  ADCSRA = ADCSRA & B01111111;
  // Disable the analog comparator
  ACSR = B10000000;
  // Disable digital input buffers on all analog input pins
  DIDR0 = DIDR0 | B00111111;

  Serial.begin(115200);
  obd.begin();

  // initializing components
  initialize();
}

void loop()
{
  uint32_t startTime = millis();

  if (!state.check(STATE_WORKING)) {
    if (state.check(STATE_SERVER_CONNECTED)) {
      Serial.println(F("LOGOUT"));
      notify(EVENT_LOGOUT);
      state.clear(STATE_SERVER_CONNECTED);
      connErrors = 0;
    }
    standby();
    initialize();
    return;
  }

  // receive network data
  recvTasks();

#if SERVER_SYNC_INTERVAL
  // check server sync signal to ensure connection consistency
  if (state.check(STATE_SERVER_CONNECTED) && millis() - lastSyncTime > 1000L * SERVER_SYNC_INTERVAL) {
    Serial.println(F("NO SYNC"));
    connErrors = MAX_CONN_ERRORS;
  }
#endif
  // deal with network errors
  if (connErrors >= MAX_CONN_ERRORS) {
    Serial.println(F("NET ERR"));
    state.clear(STATE_SERVER_CONNECTED);
    shutDownNet();
    initialize();
    return;
  }

  // collect data
  process();

  Serial.print(F("DATA:{"));
  Serial.print(cache.buffer()); 
  Serial.println('}');

  // transmit data
  if (state.check(STATE_SERVER_CONNECTED)) {
    if (cache.samples() > 0) {
      transmit();
    }
  }

  // re-try OBD connection if it is not ready
  if (!state.check(STATE_OBD_READY)) {
    if (millis() - lastOBDTry > 1000L * obdRetryInterval) {
      Serial.print(F("OBD:"));
      if (obd.testPID(PID_SPEED)) {
        state.set(STATE_OBD_READY);
        Serial.println(F("OK"));
      } else {
        Serial.println(F("NO"));
      }
      lastOBDTry = millis();
      if (obdRetryInterval < MAX_OBD_RETRY_INTERVAL) obdRetryInterval += 10;
    }
  }

  // motion controlled interval
  unsigned int motionless = (millis() - lastMotionTime) / 1000;
  bool stop = true;
  uint16_t sendingInterval = 0;
  for (byte i = 0; i < sizeof(stationaryTime) / sizeof(stationaryTime[0]); i++) {
    if (motionless < stationaryTime[i] || stationaryTime[i] == 0) {
      sendingInterval = 1000L * sendingIntervals[i];
      stop = false;
      break;
    }
  }
  if (stop) {
    state.clear(STATE_WORKING);
  } else {
    long n = startTime + sendingInterval - millis();
    if (n > 0) waitMotion(n);
  }
}
